plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.play.publisher)
    id("kotlin-kapt")
}

def releaseKeystorePath = providers.environmentVariable("RELEASE_KEYSTORE_PATH")
    .orElse(providers.gradleProperty("RELEASE_KEYSTORE_PATH"))
def releaseKeystorePassword = providers.environmentVariable("RELEASE_KEYSTORE_PASSWORD")
    .orElse(providers.gradleProperty("RELEASE_KEYSTORE_PASSWORD"))
def releaseKeyAlias = providers.environmentVariable("RELEASE_KEY_ALIAS")
    .orElse(providers.gradleProperty("RELEASE_KEY_ALIAS"))
def releaseKeyPassword = providers.environmentVariable("RELEASE_KEY_PASSWORD")
    .orElse(providers.gradleProperty("RELEASE_KEY_PASSWORD"))

def playServiceAccountJson = providers.environmentVariable("PLAY_SERVICE_ACCOUNT_JSON")
    .orElse(providers.gradleProperty("PLAY_SERVICE_ACCOUNT_JSON"))

android {
    namespace 'com.alos895.simplepos'
    compileSdk 35

    if (releaseKeystorePath.present) {
        signingConfigs {
            release {
                storeFile file(releaseKeystorePath.get())
                storePassword releaseKeystorePassword.orNull
                keyAlias releaseKeyAlias.orNull
                keyPassword releaseKeyPassword.orNull
            }
        }
    }

    defaultConfig {
        applicationId "com.alos895.simplepos"
        minSdk 24
        targetSdk 35
        versionCode 3
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (signingConfigs.findByName("release") != null) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
}

if (playServiceAccountJson.present) {
    play {
        serviceAccountCredentials.set(file(playServiceAccountJson.get()))
        track.set("internal")
    }
}

dependencies {
    implementation libs.androidx.foundation
    def room_version = "2.7.2"

    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation "androidx.compose.material:material-icons-extended:1.4.3"
    implementation 'com.google.code.gson:gson:2.10.1'

    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    implementation "androidx.room:room-guava:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    implementation "androidx.room:room-paging:$room_version"
}
